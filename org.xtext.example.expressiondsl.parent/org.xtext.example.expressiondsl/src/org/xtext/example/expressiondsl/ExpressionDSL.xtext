grammar org.xtext.example.expressiondsl.ExpressionDSL with org.eclipse.xtext.common.Terminals

generate expressionDSL "http://www.xtext.org/example/expressiondsl/ExpressionDSL"

Model:
	statements+=Statement*;

Statement:
	VariableDef				|
	ConstDef				|
	StructDef				|
	FunctionDef				|
	FunctionCallStatement	|
	VariableAssignment
;

VariableDef:
	'var' type=TYPE name=ID (options=Dim)? ';'
;

ConstDef:
	'val' type=TYPE name=ID (options=Dim)? ';'
;

StructDef:
	'struct' name=ID (options=Dim)? ';'
	subFields+=(StructSubFields)*
	'endstruct' ';'
;

SubField:
	('subf')? type=TYPE name=ID (options=Dim)? ';' 
;

StructSubFields:
	SubField | StructDef
;

FunctionDef:
	'def' type=TYPE name=ID ';'
;
 
VariableAssignment:
//  The assignment operators are:
//	-->	  = The expression is assigned to the target
//	-->	 += The expression is added to the target
//	-->	 -= The expression is subtracted from the target
//	-->  *= The target is multiplied by the expression
//	-->	 /= The target is divided by the expression
//	-->	**= The target is assigned the target raised to the power of the expression
	tgtvar=[VariableDef]
	op=('=' | '+=' | '-=' | '*=' | '/=' | '**=' )
	exp=Expression ';'
;
FunctionCallStatement:
	call=FunctionCall ";"
;

FunctionCall:
	ref=[FunctionDef]
	('('
 		(params+=Expression (':' params+=Expression)*)?
 	')')
;

VarOrArrayExp returns Expression:
	({VarExp}	ref=[VariableOrConstRef]) | 
	({ArrayExp}	ref=[VariableOrConstRef] '(' index=Expression ')')
;
//--------------------------------------------------------------------------
/* Expressions Precedence: 
 * 	--> ()
 * 	--> Built-in functions, user-defined functions
 * 	--> unary +, unary -, NOT
 * 	--> **
 * 	--> *, /
 * 	--> binary +, binary -
 * 	--> =, <>, >, >=, <, <=
 * 	--> AND
 * 	--> OR
 * 
 ***/
Expression: Or;
Or returns Expression:
	And (
		{Or.left=current} 'OR'
		right=And
	)*;
And returns Expression:
	Comparison (
		{And.left=current} 'AND'
		right=Comparison
	)*;
Comparison returns Expression:
	BinaryPlusOrMinus (
		{Comparison.left=current} op=('=' | '<>' | '>=' | '<=' | '>' | '<')
		right=BinaryPlusOrMinus
	)*;
BinaryPlusOrMinus returns Expression:
	MulOrDiv (
		({BinaryPlus.left=current} '+' | {BinaryMinus.left=current} '-')
		right=MulOrDiv
	)*;
MulOrDiv returns Expression:
	Exponent (
		({MulOrDiv.left=current} op=('*' | '/'))
		right=Exponent
	)*;
Exponent returns Expression:
	//	http://blog.efftinge.de/2010/08/parsing-expressions-with-xtext.html
	//	Right associativity is done using the following pattern (note the quantity operator and the call to the rule itself at the end)
	// '*' - Left Associativity
	// '?' - Right Associativity
	UnaryPlusMinusOrNot (
		{Exponent.left=current} '**'
		right=Exponent
	)?;
UnaryPlusMinusOrNot returns Expression:
	FunctionExp	|
	( ({UnaryPlus} '+' | {UnaryMinus} '-' | {Not} 'NOT') expr=UnaryPlusMinusOrNot )
	;
FunctionExp returns Expression:
	FunctionCall |
	Primary
	;
Primary returns Expression:
	'(' Expression ')'	|
	VarOrArrayExp		| // <<<< Issue is Here?
	Atomic
	;
Atomic returns Expression:
	{IntConstant} value=INT			|
	{StringConstant} value=STRING	|
	{BooleanConstant} value=BOOL
	;
//--------------------------------------------------------------------------
VariableOrConstRef:
	VariableDef	|
	ConstDef	|
	StructDef	|
	StructSubFields
;

Dim:
//Dimensions of Array (as in how many elements)
	'DIM' '(' dim=INT ')'
;

terminal TYPE:
	'int' | 'char' | 'bool'
;
terminal BOOL:
	'true' | 'false'
;